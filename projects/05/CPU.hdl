// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
// ALU part
    Not(in=instruction[15], out=nopcode);
    Mux16(a=alu, b=instruction, sel=nopcode, out=m1);
    Mux(a=true, b=instruction[5], sel=instruction[15], out=d1ornot);
    ARegister(in=m1, load=d1ornot, out[0..14]=addressM, out=regA);
    Mux(a=false, b=instruction[12], sel=instruction[15], out=AorM);
    Mux16(a=regA, b=inM, sel=AorM, out=m2);
    Mux(a=false, b=instruction[4], sel=instruction[15], out=d2ornot);
    DRegister(in=alu, load=d2ornot, out=regD);
    ALU(x=regD, y=m2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=alu, zr=outzr, ng=outng);

// To output writeM
    Mux(a=false, b=instruction[3], sel=instruction[15], out=writeM);

// PC part (jump only if there is a C-instruction)
    Mux(a=instruction[0], b=instruction[2], sel=outng, out=ju);
    Mux(a=ju, b=instruction[1], sel=outzr, out=jum);
    Mux(a=false, b=jum, sel=instruction[15], out=jump);
    PC(in=regA, load=jump, inc=true, reset=reset, out[0..14]=pc);

/** Logic to get load = f(ALU output bits, jump bits)
    Not(in=outzr, out=noutzr);
    Not(in=outng, out=noutng);
    And(a=noutzr, b=noutng, out=and1);
    And(a=and1, b=instruction[0], out=and2);
    And(a=instruction[1], b=outzr, out=and3);
    And(a=and3, b=noutng, out=and4);
    And(a=instruction[2], b=outng, out=and5);
    And(a=and5, b=noutzr, out=and6);
    And(a=instruction[1], b=instruction[2], out=and7);
    And(a=and7, b=outng, out=and8);
    Or(a=and2, b=and4, out=or1);
    Or(a=and6, b=and8, out=or2);
    Or(a=or1, b=or2, out=jump);    
*/       
}